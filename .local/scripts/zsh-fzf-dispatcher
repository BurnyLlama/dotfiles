#!/bin/env zsh

# Dependencies:
# * lsd
# * chafa
# * exiftool
# * bat
# * grc
# * gentoolkit
# * expect (for unbuffer command)

# Available variables:
# "desc:     $desc"
# "word:     $word"
# "group:    $group"
# "realpath: $realpath"
# "words:    $words"
# https://github.com/Aloxaf/fzf-tab/wiki/Preview#variables

source "${HOME}/.config/zsh/lib/utils.zsh"

# Exit if checking an option.
[[ $group == "[option]" ]] && exit

[[ ! -z "${realpath+x}" ]] && mimetype="$(file -bL --mime-type $realpath)"
category="${mimetype%%/*}"
filetype="${mimetype##*/}"

# List directories
[[ -d "$realpath" ]] && lsd --color always --icon always --icon-theme fancy "$realpath" && exit

# Display images
[[ $category == "image" ]] && chafa "$realpath" && exiftool "$realpath" && exit

# Text and source files
[[ $category == "text" ]] && bat --color=always "$realpath" && exit
[[ $filetype == "@(json|javascript)" ]] && bat --color=always "$realpath" && exit
[[ $realpath == *".tar."* ]] && grc -c "$HOME/.grc/conf.d/conf.tar_tvf" --colour=on -- tar tvf "$realpath" && exit

# Packages
function emerge_info() {
    check_command_exists equery || echo "Please install gentoolkit if you want information about this package."
    unbuffer equery meta $word
    unbuffer equery depgraph $word
    unbuffer equery uses $word
}

function dnf_info() {
    dnf -Cq info $word
}

function pacman_info() {
    unbuffer pacman -S --info $word
}

function package_info() {
    check_command_exists emerge && emerge_info && exit
    check_command_exists dnf && dnf_info && exit
    check_command_exists pacman && pacman_info && exit
}

[[ $group == "[package]" || $group == "[packages]" ]] && package_info && exit

# Default behaviour: file info
echo "Word: $word"
echo "File: $realpath"
echo "Mime: $mimetype"
echo "Group: $group"

